/*
 * Copyright 2015 Mark Price
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'checkstyle'

defaultTasks 'checkstyleMain', 'checkstyleTest', 'build'

version = new Version(major: 0, revision: 1)

ext {
    fullName = 'lockit'
    fullDescription = 'lockit'
    teamName = ''
    siteUrl = 'http://epickrram.com'
    sourceUrl = ''

    javaCompilerExecutable = System.env['JAVA_HOME'] ? System.env['JAVA_HOME'] + '/bin/javac' : 'javac'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }

    test {
        java {
            srcDir 'src/test/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
    }
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib'
    }
}

dependencies {
    compile 'org.bouncycastle:bcprov-jdk15on:1.52'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = project.name

    manifest.attributes('Built-By': System.properties['user.name'],
                        'Bundle-Name': fullName,
                        'Bundle-Vendor': teamName,
                        'Bundle-Description': fullDescription,
                        'Bundle-DocURL': siteUrl)
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


artifacts {
    archives sourcesJar, javadocJar
}

checkstyle {
    toolVersion = "6.3"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}

task bundleJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Lockit',
                'Implementation-Version': version,
                'Main-Class': 'com.epickrram.lockit.gui.Lockit'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    exclude "META-INF/*.RSA", "META-INF/*.SF","META-INF/*.DSA"
    with jar
}

// javapackager -createjar -appclass com.epickrram.lockit.gui.Lockit -srcdir ./out/production/lockit/ -outdir out -outfile lockit.jar -v



class Version {
    int major, minor = 0, revision = 0
    boolean snapshot
    String stage

    String toString() {
        "$major.$minor.$revision${stage ? '.' + stage : ''}${snapshot ? '-SNAPSHOT' : ''}"
    }
}

